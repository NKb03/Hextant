buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version" apply false
    id 'com.gradle.plugin-publish' version '1.3.0' apply false
    id 'io.codearte.nexus-staging' version '0.21.2'
    id 'com.github.nkb03.hextant-library' version '0.1-SNAPSHOT' apply false
    id 'com.github.nkb03.hextant-plugin' version '0.2.3-SNAPSHOT' apply false
    id 'org.openjfx.javafxplugin' version '0.1.0' apply false
}

allprojects {
    group = 'com.github.nkb03'
}

tasks.register('initialSetup') {
    group = "build setup"
    doLast {
        File launcher = new File(System.getProperty("user.home") + "/hextant" + "/launcher")
        println(launcher)
        launcher.mkdirs()
        File info = new File(launcher, "project.json")
        String contents = new String(new File(rootDir, "setup/launcher-info.json").readBytes())
        info.write(contents)
    }
}

subprojects {
    repositories {
        mavenCentral()
        mavenLocal()
    }

    apply plugin: 'kotlin'

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(19))
        }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = 19
            freeCompilerArgs += '-opt-in=kotlin.RequiresOptIn'
        }
    }
}

repositories {
    mavenCentral()
}