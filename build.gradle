buildscript {
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.17'
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.3.0"
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
    id 'maven-publish'
}


apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = "markdown"
    outputDirectory = "$buildDir/docs"
    samples = ['src/test/kotlin']
    includeNonPublic = false
    reportUndocumented = true
    impliedPlatforms = ["JVM"]

}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}


static String nikok(Object dep, Object version) {
    return "org.nikok:$dep:$version"
}

static String kotlin(Object dep) {
    return "org.jetbrains.kotlin:kotlin-$dep:1.3.0"
}

static String spek(Object dep) {
    return "org.jetbrains.spek:spek-$dep:1.1.5"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    group 'org.nikok'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }

    dependencies {
        implementation kotlin("stdlib")
        testImplementation(spek("api")) {
            exclude group: "org.jetbrains.kotlin"
        }
        testImplementation(spek("junit-platform-engine")) {
            exclude group: "org.jetbrains.kotlin"
        }
        testImplementation "com.natpryce:hamkrest:1.5.0.0"
        implementation kotlin("reflect")
        implementation nikok("kref", "1.0.0")
        implementation nikok("reaktive", "1.1-SNAPSHOT")
        implementation nikok("kserial", "1.1.2")
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
        implementation "org.controlsfx:controlsfx:8.40.14"
        implementation fileTree(dir: 'lib', include: ['*.jar'])
        implementation 'com.google.guava:guava:27.0.1-jre'
        implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
        implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
        implementation "org.jetbrains.kotlin:kotlin-script-runtime:1.3.0" 
    }
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}